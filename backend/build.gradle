plugins {
  id "scala"
  id "idea"
  id "application"
  id "com.github.johnrengelman.shadow" version "8.1.1"
}

mainClassName = "JettyLauncher"

project.ext {
  scalaBinary = "2.11"
  scalaVersion = "2.11.8"
  scalatraVersion = "2.3.1"
  javaVersion = "1.8"
  println("Common: Scala Binary: " + scalaBinary + " - Scala Version: " + scalaVersion + " - Java: " + javaVersion)
}

configurations.all {
  resolutionStrategy {
    // See reason: http://stackoverflow.com/a/23870656/992426
    force "xml-apis:xml-apis:1.4.01"
  }
}

repositories {
  mavenLocal()
  mavenCentral()
}

shadowJar {
  from(project.rootDir.absolutePath + "/backend/db") {
    include "rms.mv.db"
    into("db")
  }
  from(project.rootDir.absolutePath + "/backend/src/main") {
    include "webapp/**"
    into ""
  }
  zip64 = true
  mergeServiceFiles()
  archiveClassifier.set('')
}

tasks.withType(ScalaCompile) {
  sourceCompatibility = project.javaVersion
  targetCompatibility = project.javaVersion
  scalaCompileOptions.with {
    force = true
    additionalParameters = ["-target:jvm-1.8"]
  }
}

dependencies {
  implementation "junit:junit:4.12"
  implementation "org.scalatest:scalatest_$project.scalaBinary:3.0.0"
  implementation "org.scala-lang:scala-library:$project.scalaVersion"
  implementation "org.scala-lang:scala-compiler:$project.scalaVersion"
  implementation "org.scala-lang:scala-reflect:$project.scalaVersion"
  implementation "org.scala-lang:scalap:$project.scalaVersion"
  implementation "org.scala-lang.modules:scala-parser-combinators_$project.scalaBinary:1.0.4"
  
  // Web server
  implementation "org.scalatra:scalatra_$project.scalaBinary:$project.scalatraVersion"
  implementation "org.scalatra:scalatra-scalate_$project.scalaBinary:$project.scalatraVersion"
  implementation "org.scalatra.scalate:scalate-core_$project.scalaBinary:1.7.0"
  implementation "org.scalatra:scalatra-json_$project.scalaBinary:$project.scalatraVersion"
  implementation "org.scalatra:scalatra-auth_$project.scalaBinary:$project.scalatraVersion"
  
  implementation("javax.servlet:javax.servlet-api:3.1.0") {
    version {
      strictly("3.1.0")
    }
  }
  implementation "org.eclipse.jetty:jetty-webapp:9.3.11.v20160721"
  
  implementation "ch.qos.logback:logback-classic:1.1.2"
  implementation "com.typesafe.slick:slick_$project.scalaBinary:3.1.1"
  implementation "com.typesafe:config:1.3.0"
  implementation "com.h2database:h2:1.4.190"
  implementation "org.json4s:json4s-jackson_$project.scalaBinary:3.3.0.RC3"
  implementation "org.json4s:json4s-native_$project.scalaBinary:3.3.0.RC3"
  implementation "com.mchange:c3p0:0.9.5.1"
  
  // Mail
  implementation 'com.sendgrid:sendgrid-java:3.1.0'
  
  // Image processing
  implementation "com.sksamuel.scrimage:scrimage-core_$project.scalaBinary:2.0.1"
  implementation "com.sksamuel.scrimage:scrimage-io_$project.scalaBinary:2.0.1"
  implementation "com.sksamuel.scrimage:scrimage-filters_$project.scalaBinary:2.0.1"
  
  implementation "org.apache.xmlgraphics:batik-codec:1.7"
  implementation "me.lessis:courier_$project.scalaBinary:0.1.3"
  implementation "com.dropbox.core:dropbox-core-sdk:1.7.7"
}

test {
  testLogging {
    showStandardStreams = true
  }
}

task resolveDependencies(group: "mito", description: "Resolves all projects dependencies from the repository.") {
  doLast {
    rootProject.allprojects { project ->
      project.buildscript.configurations.forEach { configuration ->
        if (configuration.canBeResolved) {
          configuration.resolve()
        }
      }
      
      project.configurations.forEach { configuration ->
        if (configuration.canBeResolved) {
          configuration.resolve()
        }
      }
    }
  }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(8)
  }
}
